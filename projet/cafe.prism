mdp

module Utilisateur
	bouton : bool init false; // passe de F a V ou de V a F quand une action externe a lieu
	bsucre : bool init false;
	sucre : [0..2] init 0; //non sucré, sucré, très sucré
  [Bcafe] (bsucre=false) -> (bouton'=!bouton);
  [Bthe] (bsucre=false) -> (bouton'=!bouton);
  [Bann] (bsucre=false) -> (bouton'=!bouton);
  [AjoutPiece] (bsucre=false) -> (bouton'=!bouton);
  [PrendGobelet] (bsucre=false) -> (bouton'=!bouton);
  [Bsucre] (bsucre=false) -> (bsucre'=true); 
  [] (bsucre=true) & (sucre<2) -> (bsucre'=false) & (sucre'=sucre+1); //augmente sucre
  [] (bsucre=true) & (sucre=2) -> (bsucre'=false) & (sucre'=0); //augmente sucre
endmodule

module Controlleur
	p50 : bool init false;
	r50 : bool init false;

	bcafe : bool init false;
	bthe : bool init false;
	bann : bool init false;

	mode : [1..4] init 1; //dispo, choix, prepa, fin
	vpret : bool init true;
	vprepa : bool init false;
	vfini : bool init false;

	pcafe : bool init false;
	pthe : bool init false;
	gout : bool init false;

  [AjoutPiece] (mode=1) & (p50=false) & (r50=false) -> (p50'=true);
  [i] (mode=1) & (p50=true) -> (p50'=false) & (mode'=2);
  [i] (mode=1) & (r50=true) -> (r50'=false);

  [Bsucre] (mode=2) & (bann=false) -> (mode'=2);

  [Bann] (mode=2) & (bann=false) -> (mode'=2) & (bann'=true); //rend piece
  [i] (mode=2) & (bann=true) -> (mode'=1) & (r50'=true) & (bann'=false) ; //retour etat initial quand il n'y a plus de piece
  
  [Bcafe] (mode=2) & (bann=false) -> (mode'=3) & (bcafe'=true) & (vpret'=false) & (vprepa'=true);
  [Bthe] (mode=2) & (bann=false) -> (mode'=3) & (bthe'=true) & (vpret'=false) & (vprepa'=true);

  [i] (mode=3) & (bcafe=true) -> (mode'=4) & (bcafe'=false) & (pcafe'=true) & (vprepa'=false) & (vfini'=true);
  [i] (mode=3) & (bthe=true) -> (mode'=4) & (bthe'=false) & (pthe'=true) & (vprepa'=false) & (vfini'=true);
  [PrendGobelet] (mode=4) & (gout=false) & (pcafe=true) -> (gout'=true) & (pcafe'=false);
  [PrendGobelet] (mode=4) & (gout=false) & (pthe=true) -> (gout'=true) & (pthe'=false);

  [i] (mode=4) & (gout=true) -> (mode'=1) & (gout'=false) & (vfini'=false) & (vpret'=true);

endmodule

label "Mdispo" = (mode=1);
label "Mchoix" = (mode=2);
label "Mprepa" = (mode=3);
label "Mfin"   = (mode=4);
label "Vpret"  = (vpret=true);
label "Vprepa" = (vprepa=true);
label "Vfini"  = (vfini=true);

label "P50" = (p50=true);
label "R50" = (r50=true);

label "Psucre" = (sucre=1);
label "Ptres_sucre" = (sucre=2);

label "Gout" = (gout= true);
label "Pcafe" = (pcafe= true);
label "Pthe" = (pthe= true);

label "Bsucre" = (bsucre=true);
label "Bann" = (bann=true);
