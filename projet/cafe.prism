mdp

module Utilisateur
	bouton : bool init false; // passe de F a V ou de V a F quand une action externe a lieu
  [Bcafe] true -> (bouton'=!bouton);
  [Bthe] true -> (bouton'=!bouton);
  [Bann] true -> (bouton'=!bouton);
  [AjoutPiece] true -> (bouton'=!bouton);
  [PrendGobelet] true -> (bouton'=!bouton);
  [Bsucre] true -> (bouton'=!bouton); 
endmodule

module Controlleur
	piece : bool init false;
	sucre : [0..2] init 0; //non sucré, sucré, très sucré
	bcafe : bool init false;
	bthe : bool init false;

	mode : [1..4] init 1; //dispo, choix, prepa, fin
	vpret : bool init true;
	vprepa : bool init false;
	vfini : bool init false;

	pcafe : bool init false;
	pthe : bool init false;
	gout : bool init false;

  [AjoutPiece] (mode=1) & (piece=false) -> (piece'=true) & (mode'=2);

  [Bsucre] (mode=2) & (piece=true) & (sucre<2) -> (mode'=2) & (sucre'=sucre+1); //augmente sucre
  [Bsucre] (mode=2) & (piece=true) & (sucre=2) -> (mode'=2) & (sucre'=0);	//retour au sans sucre
  [Bann] (mode=2) & (piece=true) -> (piece'=false) & (mode'=2); //rend piece
  [] (mode=2) & (piece=false) -> (mode'=1); //retour etat initial quand il n'y a plus de piece
  
  [Bcafe] (mode=2) & (piece=true) -> (mode'=3) & (bcafe'=true) & (piece'=false) & (vpret'=false) & (vprepa'=true);
  [Bthe] (mode=2) & (piece=true) -> (mode'=3) & (bthe'=true) & (piece'=false) & (vpret'=false) & (vprepa'=true);

  [] (mode=3) & (bcafe=true) -> (mode'=4) & (bcafe'=false) & (pcafe'=true) & (vprepa'=false) & (vfini'=true);
  [] (mode=3) & (bthe=true) -> (mode'=4) & (bthe'=false) & (pthe'=true) & (vprepa'=false) & (vfini'=true);

  [PrendGobelet] (mode=4) & (gout=false) & (pcafe=true) -> (gout'=true) & (pcafe'=false);
  [PrendGobelet] (mode=4) & (gout=false) & (pthe=true) -> (gout'=true) & (pthe'=false);

  [] (mode=4) & (gout=true) -> (mode'=1) & (gout'=false) & (vfini'=false) & (vpret'=true);

endmodule

label "Mdispo" = (mode=1);
label "Mchoix" = (mode=2);
label "Mprepa" = (mode=3);
label "Mfin"   = (mode=4);
label "Vpret"  = (vpret=true);
label "Vprepa" = (vprepa=true);
label "Vfini"  = (vfini=true);

label "P50" = (piece=true);
label "R50" = (mode=2) & (piece=false);

label "Psucre" = (sucre=1);
label "Ptres_sucre" = (sucre=2);

label "Gout" = (gout= true);
label "Pcafe" = (pcafe= true);
label "Pthe" = (pthe= true);
